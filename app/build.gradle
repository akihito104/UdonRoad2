apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: rootProject.file("android_build.gradle")

android {
    defaultConfig {
        applicationId "com.freshdigitable.udonroad2"
        testInstrumentationRunner "com.freshdigitable.udonroad2.AppTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            testProguardFiles 'proguard-rules-test.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures.dataBinding = true

    sourceSets {
        test {
            java.srcDirs += ["src/testShared/java"]
            resources.srcDirs += ["src/testShared/resources"]
        }
        androidTest {
            java.srcDirs += ["src/testShared/java"]
            resources.srcDirs += ["src/testShared/resources"]
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':feature:timeline')
    implementation project(':feature:media')
    implementation project(':feature:oauth')
    implementation project(':candysh')
    implementation project(':data:repository')
    implementation project(':data:db')
    implementation project(':data:restclient')
    implementation project(':model')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$coreKtx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.viewpager2:viewpager2:$viewpager2_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    implementation "androidx.room:room-runtime:$room_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    implementation "io.reactivex.rxjava2:rxjava:$rx_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid_version"

    implementation "org.twitter4j:twitter4j-core:$t4j_version"

    implementation "com.jakewharton.threetenabp:threetenabp:$threetenbp_version"
    implementation "com.jakewharton.timber:timber:$timber_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"

    // test
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.google.truth:truth:$truth_version"
    testImplementation "androidx.test:core:$androidx_test_core_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.test.ext:junit:$androidx_junit_ext_version"
    testImplementation "androidx.test:runner:$androidTestRunner_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "com.google.dagger:dagger-android:$dagger_version"
    testImplementation "com.google.dagger:dagger-android-support:$dagger_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptTest "com.google.dagger:dagger-android-processor:$dagger_version"

    // android test
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation "androidx.test:core:$androidx_test_core_version"
    androidTestImplementation "androidx.test:runner:$androidTestRunner_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_rules_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_ext_version"
    androidTestImplementation "androidx.test.ext:truth:$androidx_truth_ext_version"
    androidTestImplementation "com.google.truth:truth:$truth_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    androidTestUtil "androidx.test:orchestrator:$test_orchestrator_version"
    androidTestImplementation "com.google.dagger:dagger-android:$dagger_version"
    androidTestImplementation "com.google.dagger:dagger-android-support:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger_version"
}
