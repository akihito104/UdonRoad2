apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: rootProject.file("android_build.gradle")

android {
    defaultConfig {
        applicationId "com.freshdigitable.udonroad2"
        testInstrumentationRunner "com.freshdigitable.udonroad2.AppTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        def fileAuthorityName = "${applicationId}.fileprovider"
        manifestPlaceholders = [fileAuthority: fileAuthorityName]
        buildConfigField("String", "FILE_AUTHORITY", "\"$fileAuthorityName\"")
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            testProguardFiles 'proguard-rules-test.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures.dataBinding = true

    sourceSets {
        test {
            java.srcDirs += ["src/testShared/java"]
            resources.srcDirs += ["src/testShared/resources"]
        }
        androidTest {
            java.srcDirs += ["src/testShared/java"]
            resources.srcDirs += ["src/testShared/resources"]
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':feature:timeline')
    implementation project(':feature:media')
    implementation project(':feature:oauth')
    implementation project(":feature:shortcut")
    implementation project(":feature:input")
    implementation project(':data:repository')
    implementation project(':data:local-source')
    implementation project(':data:restclient')
    implementation project(':model')

    implementation Libs.KOTLIN_STDLIB_JDK
    implementation Libs.KOTLINX_COROUTINES
    implementation Libs.KOTLINX_COROUTINES_ANDROID
    implementation Libs.KOTLINX_COROUTINES_RX2

    implementation Libs.ANDROIDX_APPCOMPAT
    implementation Libs.ANDROIDX_ACTIVITY_KTX
    implementation Libs.ANDROIDX_CORE_KTX
    implementation Libs.ANDROIDX_CONSTRAINT_LAYOUT
    implementation Libs.MATERIAL_DESIGN
    implementation Libs.ANDROIDX_VIEWPAGER2

    implementation Libs.ANDROIDX_LIFECYCLE_EXT
    implementation Libs.ANDROIDX_LIFECYCLE_LIVEDATA_KTX
    implementation Libs.ANDROIDX_LIFECYCLE_VIEWMODEL_KTX
    implementation Libs.ANDROIDX_LIFECYCLE_RX_KTX
    implementation Libs.ANDROIDX_ROOM_RUNTIME

    implementation Libs.ANDROIDX_NAVIGATION_FRAGMENT_KTX
    implementation Libs.ANDROIDX_NAVIGATION_UI_KTX

    implementation Libs.DAGGER_ANDROID
    implementation Libs.DAGGER_ANDROID_SUPPORT
    kapt Libs.DAGGER_COMPILER
    kapt Libs.DAGGER_ANDROID_PROCESSOR

    implementation Libs.RXJAVA2
    implementation Libs.RXJAVA2_ANDROID

    implementation Libs.TWITTER4J

    implementation Libs.THREETENBP
    implementation Libs.TIMBER

    debugImplementation Libs.LEAKCANARY

    // test
    testImplementation project(":test-common-jvm")
    testImplementation Libs.JUNIT
    testImplementation Libs.MOCKK
    testImplementation Libs.TRUTH
    testImplementation Libs.ANDROIDX_TEST_CORE
    testImplementation Libs.ANDROIDX_TEST_ARCH_CORE
    testImplementation Libs.ANDROIDX_TEST_EXT_JUNIT
    testImplementation Libs.ANDROIDX_TEST_RUNNER
    testImplementation Libs.ROBOLECTRIC
    testImplementation Libs.DAGGER_ANDROID
    testImplementation Libs.DAGGER_ANDROID_SUPPORT
    kaptTest Libs.DAGGER_COMPILER
    kaptTest Libs.DAGGER_ANDROID_PROCESSOR

    // android test
    androidTestImplementation project(":test-common")
    androidTestImplementation Libs.MOCKK_ANDROID
    androidTestImplementation Libs.ANDROIDX_TEST_CORE
    androidTestImplementation Libs.ANDROIDX_TEST_RUNNER
    androidTestImplementation Libs.ANDROIDX_TEST_RULES
    androidTestImplementation Libs.ANDROIDX_TEST_EXT_JUNIT
    androidTestImplementation Libs.ANDROIDX_TEST_EXT_TRUTH
    androidTestImplementation Libs.TRUTH
    androidTestImplementation Libs.ANDROIDX_TEST_ESPRESSO
    androidTestImplementation Libs.ANDROIDX_TEST_ESPRESSO_INTENTS
    androidTestUtil Libs.ANDROIDX_TEST_ORCHESTRATOR
    androidTestImplementation Libs.DAGGER_ANDROID
    androidTestImplementation Libs.DAGGER_ANDROID_SUPPORT
    kaptAndroidTest Libs.DAGGER_COMPILER
    kaptAndroidTest Libs.DAGGER_ANDROID_PROCESSOR
}
