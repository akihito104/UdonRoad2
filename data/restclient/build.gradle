apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file("android_build.gradle")

android {
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def properties = new Properties()
        def file = project.rootProject.file('local.properties')
        if (file.exists()) {
            properties.load(file.newDataInputStream())
            def CONSUMER_KEY = properties.getProperty('consumer_key')
            buildConfigField "String", "CONSUMER_KEY", "\"${CONSUMER_KEY}\""
            def CONSUMER_SECRET = properties.getProperty('consumer_secret')
            buildConfigField "String", "CONSUMER_SECRET", "\"${CONSUMER_SECRET}\""
            def ACCESS_TOKEN = properties.getProperty('access_token')
            buildConfigField "String", "ACCESS_TOKEN", "\"${ACCESS_TOKEN}\""
            def ACCESS_SECRET = properties.getProperty('access_secret')
            buildConfigField "String", "ACCESS_SECRET", "\"${ACCESS_SECRET}\""
        } else {
            buildConfigField "String", "CONSUMER_KEY", "\"\""
            buildConfigField "String", "CONSUMER_SECRET", "\"\""
            buildConfigField "String", "ACCESS_TOKEN", "\"\""
            buildConfigField "String", "ACCESS_SECRET", "\"\""
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':data:repository-api')
    implementation project(':model')

    implementation Libs.kotlin_stdlib_jdk7
    implementation Libs.kotlinx_coroutines
    implementation Libs.kotlinx_coroutines_android

    implementation Libs.androidx_appcompat

    implementation Libs.twitter4j
    implementation Libs.androidx_lifecycle_livedata_ktx

    implementation Libs.twitter4j_http_support
    implementation Libs.threetenbp

    implementation Libs.dagger_android
    implementation Libs.dagger_android_support
    kapt Libs.dagger_compiler
    kapt Libs.dagger_android_processor

    testImplementation Libs.junit
    androidTestImplementation Libs.androidx_test_runner
    androidTestImplementation Libs.androidx_test_espresso
}
